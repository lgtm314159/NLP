//
//  Patterns for appointment events
//

pattern set appoint;

appoint		:= appoint-act | appoint-pass | appoint-nom;

// pattern for active verb phrase:  appointed <person> as <position>

appoint-act	:= [constit cat=vgroup pa=[head?isa(cAppoint)]] [constit cat=ngroup]*
		  [constit cat=ngroup]:Person age? appositive?
		  ("as" | "to" [constit cat=ngroup pa=[head=position]] "of" |
		   "to" "become")?
                   [constit cat=ngroup]:Position
		  ("," [constit cat=cconj] [constit cat=ngroup]* [constit cat=ngroup]:Person age? appositive?
		   ("as" | "to" [constit cat=ngroup pa=[head=position]] "of" |
		    "to" "become")?
                   [constit cat=ngroup]:Position)*;

// pattern for passive clause:  <person> was appointed as <position>

appoint-pass	:= [constit cat=ngroup]:Person age? appositive?
		   [constit cat=vgroup-pass pa=[head?isa(cAppoint)]]
		   ("as" | "to" [constit cat=ngroup pa=[head=position]] "of"  |
		    "to" "become")?
                   [constit cat=ngroup]:Position;

// pattern for nominalization:  appointment of <person> as <position>

appoint-nom	:= [constit cat=ngroup pa=[head?isa(cAppointment)]] "of"
		   [constit cat=ngroup]:Person age?
		   //  overgeneralize:  it's "appointment as" / "promotion to"
		   ("as" | "to" [constit cat=ngroup pa=[head=position]] "of"  |
		    "to" "become" | "to") 
                   [constit cat=ngroup]:Position
		   ("," [constit cat=cconj] [constit cat=ngroup]:Person age?
		   //  overgeneralize:  it's "appointment as" / "promotion to"
		   ("as" | "to" [constit cat=ngroup pa=[head=position]] "of"  |
		    "to" "become" | "to") 
                   [constit cat=ngroup]:Position)*;

// allow , age , after a person's name

age 		:= ","  [integer] ",";

appositive := "," [constit cat=ngroup]+ ",";

// write out person and position to standard output

when appoint    write "Appointed " + Person + " as " + Position;
